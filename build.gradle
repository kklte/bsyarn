buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-release" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE")
    }
}

allprojects {
    apply plugin: 'base'
}

subprojects { subproject ->
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    version =  '0.1.0'
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/libs-release" }
    }
    dependencies {
        compile("org.springframework.data:spring-yarn-boot:2.5.0.RELEASE")
    }
    task copyJars(type: Copy) {
        from "$buildDir/libs"
        into "$rootDir/bsyarn-dist/target/bsyarn-dist/"
        include "**/*.jar"
    }
    configurations {
        compile.exclude group: "org.slf4j", module: "slf4j-log4j12"
        runtime.exclude group: "org.slf4j", module: "slf4j-log4j12"
    }
    assemble.doLast {copyJars.execute()}
}

project('bsyarn-client') {
    apply plugin: 'org.springframework.boot'
}

project('bsyarn-appmaster') {
    apply plugin: 'org.springframework.boot'
    dependencies {
        compile("org.springframework.data:spring-yarn-batch:2.5.0.RELEASE")
        runtime("org.springframework.boot:spring-boot-starter-batch:1.5.6.RELEASE")
        runtime("org.hsqldb:hsqldb:2.3.1")
        runtime("commons-dbcp:commons-dbcp:1.2.2")
    }
}

project('bsyarn-container') {
    apply plugin: 'org.springframework.boot'
    dependencies {
        compile("org.roaringbitmap:RoaringBitmap:0.6")
        compile("org.springframework.data:spring-yarn-batch:2.5.0.RELEASE")
        runtime("org.springframework.boot:spring-boot-starter-batch:1.5.6.RELEASE")
    }
}

project('bsyarn-dist') {
    dependencies {
        compile project(":bsyarn-client")
        compile project(":bsyarn-appmaster")
        compile project(":bsyarn-container")
        testCompile("org.hsqldb:hsqldb:2.3.1")
        testCompile("org.springframework.data:spring-yarn-boot-test:2.5.0.RELEASE")
        testCompile("org.hamcrest:hamcrest-core:1.2.1")
        testCompile("org.hamcrest:hamcrest-library:1.2.1")
    }
    test.dependsOn(':bsyarn-client:assemble')
    test.dependsOn(':bsyarn-appmaster:assemble')
    test.dependsOn(':bsyarn-container:assemble')
    clean.doLast {ant.delete(dir: "target")}
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
